/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id "io.gitlab.arturbosch.detekt" version "1.7.4"
    id "com.github.ben-manes.versions" version "0.28.0"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    mavenCentral()
    jcenter()
}

ext {
    ktorVersion = "1.3.2"
}

dependencies {
    configurations.all {
        exclude group: "junit" // unneeded transitive dependency
    }

    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // KTOR
    implementation "io.ktor:ktor-server-core:$ktorVersion"
    implementation "io.ktor:ktor-server-host-common:$ktorVersion"

    // TESTING
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.3"
    testImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.3"
    testImplementation "io.ktor:ktor-jackson:$ktorVersion"
    testImplementation "io.ktor:ktor-server-cio:$ktorVersion"
    testImplementation "io.ktor:ktor-server-test-host:$ktorVersion"
    testImplementation "com.koriit.kotlin:ktor-time:0.2.2"
    testImplementation "com.koriit.kotlin:slf4j-utils-logback:0.3.1"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.1"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+((-r)|(release)|(final)|(ga))?$/
    return !(version.toLowerCase() ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

targetCompatibility = 1.8
sourceCompatibility = targetCompatibility

def compilerArgs = [
        "-Xjsr305=strict"
]

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs += compilerArgs + [
                "-Xuse-experimental=io.ktor.util.KtorExperimentalAPI"
        ]
        jvmTarget = "$targetCompatibility"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.36.0"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.4"
}

if (!hasProperty("fast")) test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java

            pom {
                name = "Ktor Controllers"
                description = "Helpers to create powerful Ktor controllers"
                url = "https://github.com/Koriit/ktor-controllers"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/ktor-controllers/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = 'koriit'
                        name = 'Aleksander Stelmaczonek'
                        email = 'al.stelmaczonek@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Koriit/ktor-controllers.git'
                    developerConnection = 'scm:git:ssh://github.com/Koriit/ktor-controllers.git'
                    url = 'https://github.com/Koriit/ktor-controllers'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = findProperty("sonatype.user") ?: System.getenv("SONATYPE_USERNAME")
                password = findProperty("sonatype.password")?: System.getenv("SONATYPE_PASSWORD")
            }

            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = findProperty("signing.key") ?: System.getenv("PGP_SIGNING_KEY")
    def signingPassword = findProperty("signing.password") ?: System.getenv("PGP_SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
