/////////////
// PLUGINS //
/////////////

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.jfrog.bintray" version "1.8.4"
    id "java-library"
    id "maven-publish"
    id "jacoco"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    mavenCentral()
    jcenter()

}

ext {
    ktorVersion = "1.2.5"
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // KTOR
    implementation "io.ktor:ktor-server-core:$ktorVersion"
    implementation "io.ktor:ktor-server-host-common:$ktorVersion"

    // TESTING
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.0"
}

/////////////
// COMPILE //
/////////////

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

//////////
// TEST //
//////////

test {
    useJUnitPlatform {
        excludeTags project.hasProperty('fast') ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.4"
}

test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        bintrayMaven(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Ktor Controllers'
                description = 'Helpers to create powerful Ktor controllers'
                url = "https://github.com/Koriit/ktor-controllers"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/ktor-controllers/blob/master/LICENSE"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['bintrayMaven']

    pkg {
        repo = 'kotlin'
        name = 'ktor-controllers'
        version {
            name = project.version
            released  = new Date()
        }
    }
}

///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}
